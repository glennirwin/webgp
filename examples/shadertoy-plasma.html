<!DOCTYPE html>
<html>
<head>
	<title>WebGP - Example Simple ShaderToy</title>
	<meta charset="utf-8">
</head>
<body style="margin: 0; background-color: black;">
    <canvas></canvas>
    <script src="../src/webgp.js"></script>
    <script>
        const canvas = document.querySelector("canvas");
        //canvas.width = 700;   // Shadertoy defaults
        //canvas.height = 394;
        canvas.width = window.innerWidth - 5;
        canvas.height = window.innerHeight - 5;;

         const image1 = new Image();
         const image2 = new Image();

        const GP = WebGP(canvas);

        // Comment these to hide the log and controls
        let log = GP.Util.initializeHeadsUpLog();
        GP.Util.createShaderControls("GP");
        let stats = document.createElement("paragraph");
        stats.appendChild(document.createTextNode(""));
        GP.Util.butdiv.appendChild(stats);

        // Textures are assign with uniforms
        const uniforms = {
            iChannel0: null,
            iChannel1: null
        };

        // Mouse location and click location
        let mouse = [0.0, 0.0, 0.0, 0.0];

        // Set up a uniform block that we can update as we need
        // they will be available in the updateStep shader as u_name
        const uniformBlock = new GP.UniformBlock({
            struct: {
                iMouse: "vec4",
                iResolution: "vec2",
                iTime: "float",
                iTimeDelta: "float"
            },
            initialize: {
                iMouse: mouse,
                iResolution: [canvas.width, canvas.height],
                iTime: Date.now()/1000.0,
                iTimeDelta: 0.0
            }
        });

        // When the mouse changes, update the data in the uniform block, will be written every frame so no need to write
        window.addEventListener('mousemove', event => { mouse[0] = event.clientX; mouse[1] = event.clientY;  });
        window.addEventListener('mousedown', event => {  mouse[2] = event.clientX; mouse[3] = event.clientY;  });
        window.addEventListener('mouseup', event => { mouse[2] = 0.0; mouse[3] = 0.0; });

        // Create a vertex computer to render a quad using a fragment shader
         const toy = new GP.VertexComputer({
                units: 6,
                type: GP.gl.TRIANGLES,
                struct: { position: "vec2" },
                uniforms: uniforms,
                uniformBlock: uniformBlock,
                initializeObject: (i) => {
                    switch(i) {
                        case 0: return { position: [-1,-1] };
                        case 1: return { position: [1,-1] };
                        case 2: return { position: [-1,1] };
                        case 3: return { position: [-1,1] };
                        case 4: return { position: [1,-1] };
                        case 5: return { position: [1,1] };
                    };
                },
                renderStep: {
                    glsl: `
                        void main() {
                            gl_Position = vec4(i_position, 1.0, 1.0);
                        }   `,
                    // and give it a fragment shader
                    fragmentParams: { iChannel0: "sampler2D" },
                    fragment: `
//Plasma Globe by nimitz (twitter: @stormoid)

//looks best with around 25 rays
#define NUM_RAYS 13.

#define VOLUMETRIC_STEPS 19

#define MAX_ITER 35
#define FAR 6.

#define time u_iTime*1.1


mat2 mm2(in float a){float c = cos(a), s = sin(a);return mat2(c,-s,s,c);}
float noise( in float x ){return textureLod(u_iChannel0, vec2(x*.01,1.),0.0).x;}

float hash( float n ){return fract(sin(n)*43758.5453);}

//iq's ubiquitous 3d noise
float noise(in vec3 p)
{
	vec3 ip = floor(p);
    vec3 f = fract(p);
	f = f*f*(3.0-2.0*f);

	vec2 uv = (ip.xy+vec2(37.0,17.0)*ip.z) + f.xy;
	vec2 rg = textureLod( u_iChannel0, (uv+ 0.5)/256.0, 0.0 ).yx;
	return mix(rg.x, rg.y, f.z);
}

mat3 m3 = mat3( 0.00,  0.80,  0.60,
              -0.80,  0.36, -0.48,
              -0.60, -0.48,  0.64 );


//See: https://www.shadertoy.com/view/XdfXRj
float flow(in vec3 p, in float t)
{
	float z=2.;
	float rz = 0.;
	vec3 bp = p;
	for (float i= 1.;i < 5.;i++ )
	{
		p += time*.1;
		rz+= (sin(noise(p+t*0.8)*6.)*0.5+0.5) /z;
		p = mix(bp,p,0.6);
		z *= 2.;
		p *= 2.01;
        p*= m3;
	}
	return rz;
}

//could be improved
float sins(in float x)
{
 	float rz = 0.;
    float z = 2.;
    for (float i= 0.;i < 3.;i++ )
	{
        rz += abs(fract(x*1.4)-0.5)/z;
        x *= 1.3;
        z *= 1.15;
        x -= time*.65*z;
    }
    return rz;
}

float segm( vec3 p, vec3 a, vec3 b)
{
    vec3 pa = p - a;
	vec3 ba = b - a;
	float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1. );
	return length( pa - ba*h )*.5;
}

vec3 path(in float i, in float d)
{
    vec3 en = vec3(0.,0.,1.);
    float sns2 = sins(d+i*0.5)*0.22;
    float sns = sins(d+i*.6)*0.21;
    en.xz *= mm2((hash(i*10.569)-.5)*6.2+sns2);
    en.xy *= mm2((hash(i*4.732)-.5)*6.2+sns);
    return en;
}

vec2 map(vec3 p, float i)
{
	float lp = length(p);
    vec3 bg = vec3(0.);
    vec3 en = path(i,lp);

    float ins = smoothstep(0.11,.46,lp);
    float outs = .15+smoothstep(.0,.15,abs(lp-1.));
    p *= ins*outs;
    float id = ins*outs;

    float rz = segm(p, bg, en)-0.011;
    return vec2(rz,id);
}

float march(in vec3 ro, in vec3 rd, in float startf, in float maxd, in float j)
{
	float precis = 0.001;
    float h=0.5;
    float d = startf;
    for( int i=0; i<MAX_ITER; i++ )
    {
        if( abs(h)<precis||d>maxd ) break;
        d += h*1.2;
	    float res = map(ro+rd*d, j).x;
        h = res;
    }
	return d;
}

//volumetric marching
vec3 vmarch(in vec3 ro, in vec3 rd, in float j, in vec3 orig)
{
    vec3 p = ro;
    vec2 r = vec2(0.);
    vec3 sum = vec3(0);
    float w = 0.;
    for( int i=0; i<VOLUMETRIC_STEPS; i++ )
    {
        r = map(p,j);
        p += rd*.03;
        float lp = length(p);

        vec3 col = sin(vec3(1.05,2.5,1.52)*3.94+r.y)*.85+0.4;
        col.rgb *= smoothstep(.0,.015,-r.x);
        col *= smoothstep(0.04,.2,abs(lp-1.1));
        col *= smoothstep(0.1,.34,lp);
        sum += abs(col)*5. * (1.2-noise(lp*2.+j*13.+time*5.)*1.1) / (log(distance(p,orig)-2.)+.75);
    }
    return sum;
}

//returns both collision dists of unit sphere
vec2 iSphere2(in vec3 ro, in vec3 rd)
{
    vec3 oc = ro;
    float b = dot(oc, rd);
    float c = dot(oc,oc) - 1.;
    float h = b*b - c;
    if(h <0.0) return vec2(-1.);
    else return vec2((-b - sqrt(h)), (-b + sqrt(h)));
}

void main( )
{
	vec2 p = gl_FragCoord.xy/u_iResolution.xy-0.5;
	p.x*=u_iResolution.x/u_iResolution.y;
	vec2 um = u_iMouse.xy / u_iResolution.xy-.5;

	//camera
	vec3 ro = vec3(0.,0.,5.);
    vec3 rd = normalize(vec3(p*.7,-1.5));
    mat2 mx = mm2(time*.4+um.x*6.);
    mat2 my = mm2(time*0.3+um.y*6.);
    ro.xz *= mx;rd.xz *= mx;
    ro.xy *= my;rd.xy *= my;

    vec3 bro = ro;
    vec3 brd = rd;

    vec3 col = vec3(0.0125,0.,0.025);
    #if 1
    for (float j = 1.;j<NUM_RAYS+1.;j++)
    {
        ro = bro;
        rd = brd;
        mat2 mm = mm2((time*0.1+((j+1.)*5.1))*j*0.25);
        ro.xy *= mm;rd.xy *= mm;
        ro.xz *= mm;rd.xz *= mm;
        float rz = march(ro,rd,2.5,FAR,j);
		if ( rz >= FAR)continue;
    	vec3 pos = ro+rz*rd;
    	col = max(col,vmarch(pos,rd,j, bro));
    }
    #endif

    ro = bro;
    rd = brd;
    vec2 sph = iSphere2(ro,rd);

    if (sph.x > 0.)
    {
        vec3 pos = ro+rd*sph.x;
        vec3 pos2 = ro+rd*sph.y;
        vec3 rf = reflect( rd, pos );
        vec3 rf2 = reflect( rd, pos2 );
        float nz = (-log(abs(flow(rf*1.2,time)-.01)));
        float nz2 = (-log(abs(flow(rf2*1.2,-time)-.01)));
        col += (0.1*nz*nz* vec3(0.12,0.12,.5) + 0.05*nz2*nz2*vec3(0.55,0.2,.55))*0.8;
    }

	fragColor = vec4(col*1.3, 1.0);
}
 ` }

    });

        let watch = GP.Util.stopWatch();

        let startTime = Date.now();
        let lastTime = Date.now();
        let newTime;

        function loop() {
            watch.mark();

            let newTime = Date.now();
            uniformBlock.setWrite({
                iTimeDelta: (newTime - lastTime)/1000.0,
                iTime: (newTime - startTime)/1000.0,
                iMouse: mouse
            });
            lastTime = Date.now();

            toy.step();

            if (watch.check() % 100 === 0) {
                stats.innerText = watch.stats();  watch.reset();
            }
            GP.Util.GPControls(loop);
        }

        log("Loading images");
        let imagesToLoad = 1;
        let imagesLoaded = 0;
        let imageLoaded = function() {
            imagesLoaded++;
            log(".");
            if (imagesLoaded === imagesToLoad) {
                log("Starting the shadertoy");
                uniforms.iChannel0 = GP.Util.buildImageTexture(image1);
                loop();
            }
        };

        image1.onload = imageLoaded;
        image1.src = "noise.png";

    </script>
</body>
</html>
